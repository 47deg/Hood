buildscript {
  ext {
    arrowVersion = "0.9.0"
    http4kVersion = "3.160.1"
    kotlintestVersion = "3.3.3"
    csvVersion = "1.5"
  }
}

plugins {
  id "java-gradle-plugin"
  id "maven-publish"
  id "com.gradle.plugin-publish" version "0.11.0"
  id "org.jetbrains.kotlin.jvm" version "1.3.71"
  id "com.jfrog.bintray" version "1.8.5"
}

repositories {
  maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

  implementation "io.arrow-kt:arrow-core-data:$arrowVersion"
  implementation "io.arrow-kt:arrow-core-extensions:$arrowVersion"
  implementation "io.arrow-kt:arrow-syntax:$arrowVersion"
  implementation "io.arrow-kt:arrow-typeclasses:$arrowVersion"
  implementation "io.arrow-kt:arrow-extras-data:$arrowVersion"
  implementation "io.arrow-kt:arrow-extras-extensions:$arrowVersion"
  implementation "io.arrow-kt:arrow-effects-data:$arrowVersion"
  implementation "io.arrow-kt:arrow-effects-extensions:$arrowVersion"
  implementation "io.arrow-kt:arrow-effects-io-extensions:$arrowVersion"
  implementation "io.arrow-kt:arrow-generic:$arrowVersion"

  implementation "org.http4k:http4k-core:$http4kVersion"
  implementation "org.http4k:http4k-client-okhttp:$http4kVersion"
  implementation "org.http4k:http4k-format-jackson:$http4kVersion"

  implementation "org.apache.commons:commons-csv:$csvVersion"
  testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintestVersion"
  testImplementation "io.kotlintest:kotlintest-assertions-arrow:$kotlintestVersion"

}

sourceSets {
  main.java.srcDirs += "src/main/kotlin"
  test.java.srcDirs += "src/test/kotlin"
}

test {
  useJUnitPlatform()
}

apply from: rootProject.file("gradle/gradle-mvn-push.gradle")

gradlePlugin {
  plugins {
    hood {
      id = "com.47deg.hood"
      implementationClass = "com.fortysevendeg.hood.HoodPlugin"
    }
  }
}

pluginBundle {
  website = "https://47degrees.github.io/hood/"
  vcsUrl = "https://github.com/47degrees/hood"
  description = "The plugin to manage benchmarks on your CI"
  tags = ["kotlin", "arrow", "hood", "plugin", "benchmark", "comparison"]

  plugins {
    hood {
      displayName = "Hood Gradle Plugin"
    }
  }
}

def findPropertyOrEnv(String key) {
  [project.properties[key], System.getenv(key)].find { it != null }
}

bintray {
  publish = true
  user = findPropertyOrEnv("BINTRAY_USER") ?: "no.bintray.user"
  key = findPropertyOrEnv("BINTRAY_API_KEY") ?: "no.bintray.api.key"
  publications = ["HoodPublication"]
  configurations = ["archives"]
  pkg {
    repo = "hood"
    name = project.name
    userOrg = POM_DEVELOPER_ID
    licenses = ["Apache-2.0"]
    vcsUrl = "https://github.com/47degrees/hood.git"
  }
}
