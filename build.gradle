buildscript {
  ext {
    kotlinVersion = '1.3.21'
    arrowVersion = "0.8.2"
    http4kVersion = "3.113.0"
  }

  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
  }
}

apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

repositories {
  maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
  mavenCentral()
}

apply from: rootProject.file("gradle/gradle-mvn-push.gradle")

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

  implementation "io.arrow-kt:arrow-core:$arrowVersion"
  implementation "io.arrow-kt:arrow-data:$arrowVersion"
  implementation "io.arrow-kt:arrow-syntax:$arrowVersion"
  implementation "io.arrow-kt:arrow-typeclasses:$arrowVersion"
  implementation "io.arrow-kt:arrow-effects:$arrowVersion"
  implementation "io.arrow-kt:arrow-effects-instances:$arrowVersion"
  implementation "io.arrow-kt:arrow-data-instances-effects:$arrowVersion"

  implementation "org.http4k:http4k-core:$http4kVersion"
  implementation "org.http4k:http4k-client-okhttp:$http4kVersion"
  implementation "org.http4k:http4k-format-jackson:$http4kVersion"

  implementation "org.apache.commons:commons-csv:1.5"
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin'
  test.java.srcDirs += 'src/test/kotlin'
}

gradlePlugin {
  plugins {
    hood {
      id = "com.47deg.hood"
      implementationClass = "com.fortysevendeg.hood.HoodPlugin"
    }
  }
}

def findPropertyOrEnv(String key) {
  [project.properties[key], System.getenv(key)].find { it != null }
}

bintray {
  publish = true
  user = findPropertyOrEnv("BINTRAY_USER") ?: "no.bintray.user"
  key = findPropertyOrEnv("BINTRAY_API_KEY") ?: "no.bintray.api.key"
  publications = ['MyPublication']
  configurations = ['archives']
  pkg {
    repo = 'hood'
    name = project.name
    userOrg = POM_DEVELOPER_ID
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/47deg/hood.git'
  }
}

